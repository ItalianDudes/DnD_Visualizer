plugins {
    id 'com.gradleup.shadow' version '8.3.5'
    id 'java'
}

// Versioning
def versionNumber = '1.3.9.3'
def versionType = 'RELEASE' // RELEASE | BETA | ALPHA | DEV
def versionMarker = versionType.charAt(0)

group = 'it.italiandudes'
version = versionNumber+versionMarker

repositories {
    mavenCentral()
    flatDir {
        dirs 'lib'
    }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:26.0.1'
    implementation 'commons-io:commons-io:2.18.0'
    implementation 'commons-codec:commons-codec:1.17.1'
    implementation 'it.italiandudes:idl:1.0.9'
    implementation 'org.json:json:20240303'
    implementation 'club.minnced:java-discord-rpc:2.0.1'
    implementation 'org.xerial:sqlite-jdbc:3.47.1.0'
    implementation 'org.slf4j:slf4j-nop:2.0.16'
}

shadowJar {
    manifest {
        attributes "Main-Class" : "it.italiandudes.dnd_visualizer.DnD_Visualizer"
        attributes "Title" : "D&D Visualizer"
        attributes "Vendor" : "ItalianDudes"
        attributes "Version" : archiveVersion.get()
        attributes "Version-Number" : archiveVersion.get()
        attributes "Version-Type" : versionType
        attributes "Version-Marker" : versionMarker
        attributes "Timestamp" : new Date().format("dd/MM/yyyy HH:mm:ss.sss")
        attributes "ID-Launcher-Enabled" : true
    }
    archiveFileName = getRootDir().getName()+"-"+archiveVersion.get()+".jar"
}

tasks.register('buildJar') {
    dependsOn shadowJar
    group = "Build"
    description = "Build and assemble DnD_Visualizer"
}

tasks.register('runClient', Exec) {
    dependsOn shadowJar
    group = "Execution"
    description = "Build, assemble and run DnD_Visualizer"
    standardInput = System.in
    commandLine "java", "-jar", jar.archiveFile.get()
}